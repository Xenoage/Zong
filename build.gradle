// structure of Kotlin build files inspired by
// https://github.com/mverleg/kotlin_multiplatform_gradle_demo/
// TODO: give thanks to the author for his useful tips

buildscript {
	ext.kotlin_version = '1.2.30'

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

apply plugin: 'kotlin-platform-common'

/* Make a list of submodules organized by platform. */
def fmtMods = { mods -> mods.collect { it.toString().substring(8) }.join(', ') } //substring: omit "project "
project.ext {
	common_submods = subprojects.findAll {
		!it.name.endsWith('-jvm') && !it.name.endsWith('-js') // && !it.name.endsWith('-native')
	}
	println("common submodules ${fmtMods(common_submods)}")
	jvm_submods = subprojects.findAll {
		it.name.endsWith('-jvm')
	}
	println("JVM submodules: ${fmtMods(jvm_submods)}")
	js_submods = subprojects.findAll {
		it.name.endsWith('-js')
	}
	println("javascript submodules: ${fmtMods(js_submods)}")
}

/* All the projects (including this top-level one). */
allprojects {
	group 'com.xenoage.zong'
	version 'unspecified'

	repositories {
		mavenCentral()
	}

	/* Merge all the build directories into one. */
	//buildDir = rootProject.file('build')
}

/* All the subprojects (but not this top-level one, which contains no code). */
configure(subprojects) {
//    repositories {
//        mavenCentral()
//    }
}

/* All the common subprojects (including this top-level one), but no platform projects. */
configure(common_submods) {
	apply plugin: 'kotlin-platform-common'

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
		testCompile "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
		testCompile "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
	}

	sourceSets { //can not be applied to all subprojects at once, since plugin has to be applied before
		main {
			kotlin { srcDir 'src' }
			resources { srcDir 'src' }
		}
		test {
			kotlin { srcDir 'test' }
			resources { srcDir 'test' }
		}
	}
}

/* All JVM platform subprojects. */
configure(jvm_submods) {
	apply plugin: 'kotlin-platform-jvm'

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
		testCompile "junit:junit:4.12"
		testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
		testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
	}

	sourceSets { //can not be applied to all subprojects at once, since plugin has to be applied before
		main {
			kotlin { srcDir 'src' }
			resources { srcDir 'src' }
		}
		test {
			kotlin { srcDir 'test' }
			resources { srcDir 'test' }
		}
	}
}

/* All javascript platform subprojects. */
configure(js_submods) {
	apply plugin: 'kotlin-platform-js'

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
		testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
	}

	compileKotlin2Js {
		kotlinOptions {
			outputFile = "${project.buildDir.path}/js/${project.name}.js"
			moduleKind = "umd"
			sourceMap = true
			metaInfo = true
		}
	}

	sourceSets { //can not be applied to all subprojects at once, since plugin has to be applied before
		main {
			kotlin { srcDir 'src' }
			resources { srcDir 'src' }
		}
		test {
			kotlin { srcDir 'test' }
			resources { srcDir 'test' }
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.6'
}

dependencies {
	compile project(":core")
}